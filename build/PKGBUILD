pkgname="emacs-git"
pkgver=29.0.50.2
pkgrel=1
pkgdesc="GNU Emacs from git"
arch=('x86_64')
url="http://www.gnu.org/software/emacs/"
license=('GPL3')
depends=('libgccjit' 'm17n-lib' 'libotf' 'harfbuzz' 'cairo' 'jansson')
makedepends=('git')
provides=('emacs' 'emacs-28-git')
conflicts=('emacs' 'emacs26-git' 'emacs-27-git' 'emacs-seq' 'emacs-nox')
replaces=('emacs' 'emacs26-git' 'emacs-27-git' 'emacs-seq' 'emacs-nox')
source=("repo::git://git.savannah.gnu.org/emacs.git")
# source=("repo::git://github.com/flatwhatson/emacs.git#branch=pgtk-nativecomp")
# source=("repo::git://git.savannah.gnu.org/emacs.git#branch=feature/pgtk"")
# source=("repo::git://github.com/emacs-mirror/emacs.git")
options=(!strip)
b2sums=('SKIP')


pkgver() {
    cd "$srcdir/repo"
    printf "%s.%s" \
           $(sed -n '/AC_INIT/s/.*GNU Emacs, \([0-9.]*\).*/\1/p' configure.ac) \
           $(git rev-list --count HEAD)
}


prepare() {
    cd "$srcdir/repo"
    if [[ ! -x configure ]]; then
        ./autogen.sh git
        ./autogen.sh autoconf
    fi
}


build() {
    cd "$srcdir/repo"

    # export CFLAGS=" -g"
    # export CXXFLAGS=" -g"

    # gold linker
    #
    # TLDR; gold is around 2x times faster than GNU binutils linker. The
    # gold linker was designed as an ELF-specific linker unlink ld which
    # has support formats uses relies on heavy abstraction resulting more
    # memory and CPU resources.
    # export LD='/usr/bin/ld.gold'
    # export CFLAGS+=" -fuse-ld=gold"
    # export CXXFLAGS+=" -fuse-ld=gold"

    # fuse
    #
    # link-time optimization
    # export CFLAGS+=" -flto"
    # export CXXFLAGS+=" -flto"

    CONF_ARGS=(
        --prefix=/usr
        --sysconfdir=/etc
        --libexecdir=/usr/lib
        --localstatedir=/var
        --mandir=/usr/share/man

        --disable-build-details
        --enable-link-time-optimization # LTO

        --with-modules
        --with-sound=alsa

        --with-json                     # default after 27.1, needs jansson
        --with-native-compilation       # JIT yeah! needs libgccjit
        --with-file-notification=yes

        --with-pgtk                     # needed or ibus integration
        --with-cairo                    # needs cairo
        --with-harfbuzz                 # default after 27.1
        --with-x-toolkit=gtk3           # {no, gtk3, lucid}
        --without-gconf                 # breaks set font from config
        --without-gsettings             # breaks set font from config

        # --with-xwidgets               # requires --with-x-toolkit=gtk3
        --without-compress-install
        --without-xaw3d
        --with-gameuser=no
    )
    ./configure "${CONF_ARGS[@]}"

    # https://wiki.archlinux.org/title/Makepkg#Parallel_compilation

    # min elisp modue pre-compile deferred for later
    make -j$(nproc) NATIVE_FAST_BOOT=1

    # native compile ahead-of-time
    # make -j$(nproc) NATIVE_FULL_AOT=1
}


package() {
    cd "$srcdir/repo"

    make DESTDIR="$pkgdir/" install

    find "$pkgdir"/usr | xargs chown root:root
}


# Local Variables:
# mode: shell-script
# End:
